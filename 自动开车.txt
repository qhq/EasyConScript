#自动开车 200228.14.10
#此版本为刷当前帧宝可梦
#站在坑前，连不连网的状态都可以
#联网等待可根据自身联网速度调整
#招募待机处由于准备预留了10秒，需要的话待机时间可减去10秒，不要超过170秒
#进场动画可根据实际调整，需要放一遍技能的可适当长一些
#停车时间根据自身机器反应调整

#唤醒
FOR 10
    LCLICK
    300
NEXT

#密码大于9999表示无密码车
_开车密码 = 10000
_联网等待 = 20
_招募待机 = 20
_进场动画 = 20
_停车时间 = 15

FOR
    $1 += 1
    PRINT 当前车次： & $1
    #联网
    Y
    800
    PLUS
    PRINT 正在连接……
    $2 = _联网等待
    FOR $2
        1000
    NEXT
    PRINT 连接结束……
    B
    400
    B
    400
    B
    1000
    #联网

    #进坑，过滤掉领取瓦特
    A
    500
    A
    500
    A
    PRINT 输入密码或开始游戏
    4000
    #进坑

    $6 = _开车密码
    IF $6 <= 9999
        #输密码
        PLUS
        800
	PRINT 开始输入密码
	# 解析密码数字
	#$6 = $1
	$7 = 10
	FOR 4
	    $5 = $6
	    $5 %= $7
	    PUSH $5
	    $6 /= $7
	NEXT
	$5 = 0    # 记录X坐标
	$6 = 0    # 记录Y坐标
	# 循环输入密码
	FOR 4
	    POP $3
	    PRINT 输入： & $3
	    IF $3 = $0
	        $3 = 11    # 对0作特殊处理，0的位置相当于11
	    ENDIF
	    $3 -= 1
	    $4 = $3
	    $7 = 3
	    $3 %= $7    # 当前数字X坐标
	    $4 /= $7    # 当前数字Y坐标
	    $6 = -$6
	    $6 += $4
	    FOR $6
	        DOWN
	        100
	    NEXT
	    $6 = -$6
	    FOR $6
	        UP
	        100
	    NEXT
	    $6 = $4
	    IF $4 != 3    # 0的话不需要左右
	        $5 = -$5
	        $5 += $3
	        FOR $5
	            RIGHT
	            100
	        NEXT
	        $5 = -$5
	        FOR $5
	            LEFT
	            100
	        NEXT
	    ENDIF
	    $5 = $3
	    A
	    100
	NEXT
	PRINT 输入完成
        PLUS
        1000
        A
        500
        #输密码
    ENDIF

    #招募
    PRINT 准备大家一起挑战！
    A
    500
    A
    500
    A
    2000
    PRINT 等待玩家进入……
    $2 = _招募待机
    FOR $2
        1000
    NEXT
    UP #准备就绪，提醒未准备的还剩10秒准备
    500
    A
    10000 #待机10秒
    PRINT 准备开始游戏
    FOR 14 #多按几次A，防止有人太晚准备
        A
        500
    NEXT
    #招募

    #进场
    PRINT 播放进场动画……
    $2 = _进场动画
    FOR $2
        1000
    NEXT
    PRINT 开始选择技能
    FOR 15
        A
        200
    NEXT
    #进场

    #退场
    PRINT 退场ing……
    HOME 1000
    LS DOWN,800
    A
    200
    A
    300
    FOR 8
        B
        500
    NEXT
    #退场

    #停车待机
    $2 = _停车时间
    PRINT 停车 & $2 & 秒后继续发车
    FOR $2
        1000
    NEXT
    #停车
NEXT
