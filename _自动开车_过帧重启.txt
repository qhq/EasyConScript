#自动过N帧开车 200228.14.35
#此版本为重开游戏自动刷N帧后开随机宝可梦
#面对坑，未联网状态
#联网等待可根据自身联网速度调整
#招募待机处由于准备预留了10秒，需要的话待机时间可减去10秒，不要超过170秒
#进场动画可根据实际调整，需要放一遍技能的可适当长一些
#停车时间根据自身机器反应调整
#请确保当前日+过帧数不出现倒日期

#唤醒
FOR 10
    LCLICK
    300
NEXT

#密码大于9999表示无密码车
_开车密码 = 10000
_联网等待 = 30
_招募待机 = 60
_进场动画 = 15
_需过帧数 = 4

$1 = _开车密码

FOR
    $2 = _需过帧数
    IF $2 >= 1
        FOR $2
            A
            300
            A
            300
            A
            400
            A
            200
            A
            2000
            HOME
            1000
            DOWN
            50
            RIGHT
            50
            RIGHT
            50
            RIGHT
            50
            RIGHT
            200
            A
            500
            LS DOWN,1700
            LS RESET
            200
            RIGHT
            200
            DOWN
            50
            DOWN
            50
            DOWN
            50
            DOWN
            200
            A
            200
            DOWN
            50
            DOWN
            50
            A
            200
            RIGHT
            50
            RIGHT
            50
            UP
            200
            RIGHT
            50
            RIGHT
            50
            RIGHT
            50
            A
            200
            HOME
            500
            A
            1000
            B
            1000
            A
            4000
        NEXT
    ENDIF
    
    #联网
    Y
    800
    PLUS
    PRINT 正在连接…
    $2 = _联网等待
    FOR $2
        1000
    NEXT
    PRINT 连接结束…
    B
    400
    B
    400
    B
    1000
    #联网
    
    #进坑，过滤掉领取瓦特
    A
    500
    A
    500
    A
    4000
    #进坑
    
    $6 = _开车密码
    IF $6 <= 9999
        #输密码
        PLUS
        800
        PRINT 开始输入密码
        # 解析密码数字
        #$6 = _开车密码
        $7 = 10
        FOR 4
            $5 = $6
            $5 %= $7
            PUSH $5
            $6 /= $7
        NEXT
        $5 = 0    # 记录X坐标
        $6 = 0    # 记录Y坐标
        # 循环输入密码
        FOR 4
            POP $3
            PRINT 输入： & $3
            IF $3 = $0
                $3 = 11    # 对0作特殊处理，0的位置相当于11
            ENDIF
            $3 -= 1
            $4 = $3
            $7 = 3
            $3 %= $7    # 当前数字X坐标
            $4 /= $7    # 当前数字Y坐标
            $6 = -$6
            $6 += $4
            FOR $6
                DOWN
                100
            NEXT
            $6 = -$6
            FOR $6
                UP
                100
            NEXT
            $6 = $4
            IF $4 != 3    # 0的话不需要左右
                $5 = -$5
                $5 += $3
                FOR $5
                    RIGHT
                    100
                NEXT
                $5 = -$5
                FOR $5
                    LEFT
                    100
                NEXT
            ENDIF
            $5 = $3
            A
            100
        NEXT
        PRINT 输入完成
        PLUS
        1000
        A
        500
        #输密码
    ENDIF
    
    #招募
    PRINT 大家一起挑战！
    A
    250
    A
    250
    A
    2000
    PRINT 等待玩家进入…
    $2 = _招募待机
    FOR $2
        1000
    NEXT
    UP #准备就绪，提醒未准备的还剩10秒准备
    500
    A
    10000 #待机10秒
    PRINT 开始对战
    FOR 15 #多按几次A，防止有人太晚准备
        A
        200
    NEXT
    #招募
    
    #进场
    PRINT 播放进场动画…
    $2 = _进场动画
    FOR $2
        A #穿插A防止太晚准备也能A出技能
        1000
    NEXT
    PRINT 疯狂输出
    FOR 15
        A
        200
    NEXT
    #进场
    
    PRINT 准备退场
    HOME
    800
    X
    500
    A
    3000
    A
    1000
    A
    15000
    A
    8000
    #退场
NEXT
