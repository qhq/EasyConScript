#版本：200316.19.01
#准备：站在坑前，连不连网的状态都可以
#说明：此版本为自动过指定帧数刷N+1帧宝可梦，每次重启，方便长时间无人值守开车
#联网等待可根据自身联网速度调整
#招募待机处由于准备预留了10秒，需要的话待机时间可减去10秒，不要超过170秒
#进场动画可根据实际调整，需要放一遍技能的可适当长一些
#停车时间根据自身机器反应调整
#可设定N车后处理好友申请，不需要的请设置大数字即可，不可为0
#每次开完车都会重启游戏，所以不用担心掉网、卡门等原因造成操作混乱
#脚本超长，仅支持联机模式

#唤醒
FOR 10
    B
    300
NEXT

#大于9999无密码车，等于11111随机密码，等于22222三选一
_开车密码 = 12345
_联网等待 = 20
_招募待机 = 50
_进场动画 = 15
_好友申请 = 500
_需过帧数 = 4

FOR
    $1 += 1
    PRINT ----- & $1 & -----
    $2 = _需过帧数
    IF $2 >= 1
        $2 += 1
        $6 = 0
        FOR $2
            $6 += 1
            A
            300
            A
            300
            A
            400
            A
            200
            A
            2000
            #修改时间
            HOME
            1000
            DOWN
            50
            RIGHT
            50
            RIGHT
            50
            RIGHT
            50
            RIGHT
            200
            A
            500
            LS DOWN,1700
            LS RESET
            200
            RIGHT
            200
            DOWN
            50
            DOWN
            50
            DOWN
            50
            DOWN
            200
            A
            200
            DOWN
            50
            DOWN
            50
            IF $6 < $2
                LS RIGHT
                A
                200
                UP
                650
                A
                200
                LS RESET
            ELSE
                PRINT 过帧结束，恢复日期
                A
                200
                $2 += -1
                FOR $2
                    DOWN
                    100
                NEXT
                LS RIGHT
                800
                A
                200
                LS RESET
            ENDIF
            HOME
            800
            A
            1000
            B
            1000
            A
            4000
        NEXT
    ENDIF
    
    1500
    #联网
    Y
    800
    PLUS
    PRINT 正在连接…
    $2 = _联网等待
    FOR $2
        1000
    NEXT
    PRINT 连接结束
    B
    400
    B
    400
    B
    1000
    #联网
    
    #进坑
    A
    400
    A
    400
    A
    4000
    #进坑
    
    #密码模块
    $6 = _开车密码
    IF $6 = 11111
        $6 = 10000
        RAND $6
    ENDIF
    IF $6 = 22222
        $6 = 3
        RAND $6
        IF $6 = 0
            $6 = 5555
        ENDIF
        IF $6 = 1
            $6 = 7317
        ENDIF
        IF $6 = 2
            $6 = 9527
        ENDIF
    ENDIF
    
    IF $6 <= 9999
        #输密码
        PLUS
        800
        PRINT 本车密码： & $6
        # 解析密码数字
        #$6 = _开车密码
        $7 = 10
        FOR 4
            $5 = $6
            $5 %= $7
            PUSH $5
            $6 /= $7
        NEXT
        $5 = 0    # 记录X坐标
        $6 = 0    # 记录Y坐标
        # 循环输入密码
        FOR 4
            POP $3
            #PRINT 输入： & $3
            IF $3 = $0
                $3 = 11    # 对0作特殊处理，0的位置相当于11
            ENDIF
            $3 -= 1
            $4 = $3
            $7 = 3
            $3 %= $7    # 当前数字X坐标
            $4 /= $7    # 当前数字Y坐标
            $6 = -$6
            $6 += $4
            FOR $6
                DOWN
                100
            NEXT
            $6 = -$6
            FOR $6
                UP
                100
            NEXT
            $6 = $4
            IF $4 != 3    # 0的话不需要左右
                $5 = -$5
                $5 += $3
                FOR $5
                    RIGHT
                    100
                NEXT
                $5 = -$5
                FOR $5
                    LEFT
                    100
                NEXT
            ENDIF
            $5 = $3
            A
            100
        NEXT
        #PRINT 输入完成
        PLUS
        1000
        A
        500
        #输密码
    ENDIF
    #密码模块
    
    #招募
    PRINT 大家一起挑战！
    A
    250
    A
    250
    A
    2000
    PRINT 等待玩家进入…
    $2 = _招募待机
    FOR $2
        1000
    NEXT
    UP #准备就绪，提醒未准备的还剩10秒准备
    500
    A
    10000 #待机10秒
    PRINT 开始对战
    FOR 15 #多按几次A，防止有人太晚准备
        A
        200
    NEXT
    #招募
    
    #进场
    PRINT 播放进场动画…
    $2 = _进场动画
    FOR $2
        A #穿插A防止太晚准备也能A出技能
        1000
    NEXT
    PRINT 疯狂输出
    FOR 15
        A
        200
    NEXT
    #进场
    
    #退场
    PRINT 重启游戏
    HOME
    1000
    X
    500
    A
    3000
    A		#关闭游戏
    1000
    A		#启动游戏
    15000
    A		#进入游戏
    8000
    #退场
    
    #处理好友申请
    $2 = $1
    $2 %= _好友申请
    IF $2 = 0
        PRINT 好友申请处理中…
        HOME
        800
        UP
        200
        A
        1000
        A
        300
        A
        300
        LEFT
        200
        LEFT
        200
        LS DOWN,1000
        LS RESET
        UP
        200
        FOR 40	#好友申请处理时间，按需调整
            A
            500
        NEXT
        HOME
        2000
        A
        2000
    ENDIF
    #处理好友申请
NEXT
